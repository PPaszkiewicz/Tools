// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext{
        kotlin_version = '1.8.21'
        coroutines_version = '1.7.1'
        appCompat_version = '1.6.1'
        fragment_version = '1.5.7'
        constraint_version = '2.1.4'
        lifecycle_version = '2.6.1'

        jvm_toolchain_ver = 17
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    ext {
        compile_and_target_sdk = 33
    }
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// publish all modules that are android libraries and are marked for publishing with maven-publish plugin
subprojects {
    afterEvaluate {
        if (plugins.hasPlugin('com.android.library') && plugins.hasPlugin('maven-publish')) {
            task sourcesJar(type: Jar) {
                from android.sourceSets.main.java.srcDirs
            }

            artifacts {
                archives sourcesJar
            }

            afterEvaluate {
                publishing {
                    publications {
                        release(MavenPublication) {
                            from components.release
                            groupId = 'com.github.PPaszkiewicz.Tools'
                            //artifactId = 'moduleName' // inherits module name
                            // version used is in most cases in line with current Kotlin version
                            version = '1.8.21'

                            artifact(sourcesJar)
                        }
                    }
                }
            }
        }
    }
}